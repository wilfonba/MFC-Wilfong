!>
!! @file
!! @brief Contains module m_sim_helpers

#:include 'case.fpp'
#:include 'macros.fpp'

!> @brief Simulation helper routines for cell state, CFL calculation, and stability checks
module m_sim_helpers

    use m_derived_types
    use m_global_parameters
    use m_variables_conversion
    use m_thermochem, only: num_species, get_mixture_specific_heat_cv_mass, get_mixture_thermal_conductivity_mixavg, &
        & get_mixture_viscosity_mixavg
    use m_thermochem_state, only: get_mixavg_transport_state

    implicit none

    private; public :: s_compute_cell_state, s_compute_cell_diffusivity, s_compute_stability_from_dt, s_compute_dt_from_cfl, &
        & dt_limiter, dt_limiter_names

    !> Criterion currently limiting the adaptive time step (ICFL, VCFL, CCFL, TCFL, the collision cap, or the ramp limiter)
    character(len=4)                          :: dt_limiter = 'none'
    character(len=4), dimension(5), parameter :: dt_limiter_names = (/'ICFL', 'VCFL', 'CCFL', 'TCFL', 'COLL'/)

    !> Volume fraction below which a phase counts as absent for the capillary time-step limit
    real(wp), parameter :: capillary_alpha_min = 1.e-3_wp

contains

    !> Brackbill's capillary density (rho_1 + rho_2)/2 from the phase densities of an interface cell. Zero where a phase is absent,
    !! so single-phase cells, which carry no capillary force, impose no capillary limit; the local mixture density would instead let
    !! the lighter phase's cells set a far smaller step
    function f_capillary_rho(alpha, alpha_rho) result(rho_c)

        $:GPU_ROUTINE(parallelism='[seq]')
        #:if not MFC_CASE_OPTIMIZATION and USING_AMD
            real(wp), dimension(3), intent(in) :: alpha, alpha_rho
        #:else
            real(wp), dimension(num_fluids), intent(in) :: alpha, alpha_rho
        #:endif
        real(wp) :: rho_c
        integer  :: i

        rho_c = 0._wp
        if (minval(alpha(1:num_fluids)) < capillary_alpha_min) return
        $:GPU_LOOP(parallelism='[seq]')
        do i = 1, num_fluids
            rho_c = rho_c + alpha_rho(i)/alpha(i)
        end do
        rho_c = rho_c/real(num_fluids, wp)

    end function f_capillary_rho

    !> Computes the modified dtheta for Fourier filtering in azimuthal direction
    function f_compute_filtered_dtheta(k, l) result(fltr_dtheta)

        $:GPU_ROUTINE(parallelism='[seq]')
        integer, intent(in) :: k, l
        real(wp)            :: fltr_dtheta
        integer             :: Nfq

        if (grid_geometry == 3) then
            if (k == 0) then
                fltr_dtheta = 2._wp*pi*y_cb(0)/3._wp
            else if (k <= fourier_rings) then
                Nfq = min(floor(2._wp*real(k, wp)*pi), (p + 1)/2 + 1)
                fltr_dtheta = 2._wp*pi*y_cb(k - 1)/real(Nfq, wp)
            else
                fltr_dtheta = y_cb(k - 1)*dz(l)
            end if
        else
            fltr_dtheta = 0._wp
        end if

    end function f_compute_filtered_dtheta

    !> Sum over directions of 1/spacing^2 at cell (j, k, l). An explicit diffusion with diffusivity D is RK-stable for D*dt*sum <=
    !! 2.51/4 (RK3; 2/4 for RK1 and RK2), so the diffusive CFL numbers are D*dt*sum
    function f_inv_dx2_sum(j, k, l) result(s)

        $:GPU_ROUTINE(parallelism='[seq]')
        integer, intent(in) :: j, k, l
        real(wp)            :: s

        s = 1._wp/dx(j)**2
        if (n > 0) s = s + 1._wp/dy(k)**2
        if (p > 0) then
            if (grid_geometry == 3) then
                s = s + 1._wp/f_compute_filtered_dtheta(k, l)**2
            else
                s = s + 1._wp/dz(l)**2
            end if
        end if

    end function f_inv_dx2_sum

    !> Momentum diffusivity of the viscous stress: normal stresses diffuse with (4/3 mu + mu_b)/rho, the largest coefficient
    function f_visc_diffusivity(Re_l, rho) result(nu)

        $:GPU_ROUTINE(parallelism='[seq]')
        real(wp), dimension(2), intent(in) :: Re_l
        real(wp), intent(in)               :: rho
        real(wp)                           :: nu

        nu = (4._wp/(3._wp*Re_l(1)) + 1._wp/Re_l(2))/rho

    end function f_visc_diffusivity

    !> Computes the mixture coefficients, velocity and pressure of one cell
    subroutine s_compute_cell_state(q_prim_vf, pres, rho, gamma, pi_inf, Re, alpha, alpha_rho, vel, vel_sum, qv, j, k, l)

        $:GPU_ROUTINE(function_name='s_compute_cell_state',parallelism='[seq]', cray_inline=True)

        type(scalar_field), intent(in), dimension(sys_size) :: q_prim_vf
        #:if not MFC_CASE_OPTIMIZATION and USING_AMD
            real(wp), intent(inout), dimension(3) :: alpha, alpha_rho
            real(wp), intent(inout), dimension(3) :: vel
        #:else
            real(wp), intent(inout), dimension(num_fluids) :: alpha, alpha_rho
            real(wp), intent(inout), dimension(num_vels)   :: vel
        #:endif
        real(wp), intent(inout)               :: rho, gamma, pi_inf, vel_sum, pres
        real(wp), intent(out)                 :: qv
        integer, intent(in)                   :: j, k, l
        real(wp), dimension(2), intent(inout) :: Re
        #:if not MFC_CASE_OPTIMIZATION and USING_AMD
            real(wp), dimension(3) :: Gs
        #:else
            real(wp), dimension(num_fluids) :: Gs
        #:endif
        real(wp) :: G_local
        integer  :: i

        call s_compute_species_fraction(q_prim_vf, j, k, l, alpha_rho, alpha)

        if (hypoelasticity) then
            call s_convert_species_to_mixture_variables_kernel(rho, gamma, pi_inf, qv, alpha, alpha_rho, Re, G_local, Gs)
        else
            call s_convert_species_to_mixture_variables_kernel(rho, gamma, pi_inf, qv, alpha, alpha_rho, Re)
        end if

        if (igr) then
            $:GPU_LOOP(parallelism='[seq]')
            do i = 1, num_vels
                vel(i) = q_prim_vf(eqn_idx%cont%end + i)%sf(j, k, l)/rho
            end do
        else
            $:GPU_LOOP(parallelism='[seq]')
            do i = 1, num_vels
                vel(i) = q_prim_vf(eqn_idx%cont%end + i)%sf(j, k, l)
            end do
        end if

        vel_sum = 0._wp
        $:GPU_LOOP(parallelism='[seq]')
        do i = 1, num_vels
            vel_sum = vel_sum + vel(i)**2._wp
        end do

        if (igr) then
            pres = (q_prim_vf(eqn_idx%E)%sf(j, k, l) - pi_inf - qv - 5.e-1_wp*rho*vel_sum)/gamma
        else
            pres = q_prim_vf(eqn_idx%E)%sf(j, k, l)
        end if

    end subroutine s_compute_cell_state

    !> Computes stability criterion for a specified dt
    !> Thermal diffusivity of cell (j, k, l) for the explicit diffusion step limit, zero without diffusion: k/(rho cv) of Fourier
    !! conduction, or for a diffusing reacting mixture its constant-volume lambda/(rho cv), raised to the largest species
    !! diffusivity with mixture-averaged transport. A viscous reacting mixture's Re(1) becomes the 1/mu its viscous flux uses
    subroutine s_compute_cell_diffusivity(q_prim_vf, q_T_sf, pres, rho, alpha, alpha_rho, Re, Dth, j, k, l)

        $:GPU_ROUTINE(function_name='s_compute_cell_diffusivity', parallelism='[seq]', cray_inline=True)

        type(scalar_field), dimension(sys_size), intent(in) :: q_prim_vf
        type(scalar_field), intent(in)                      :: q_T_sf
        real(wp), intent(in)                                :: pres, rho
        #:if not MFC_CASE_OPTIMIZATION and USING_AMD
            real(wp), dimension(3), intent(in) :: alpha, alpha_rho
        #:else
            real(wp), dimension(num_fluids), intent(in) :: alpha, alpha_rho
        #:endif
        real(wp), dimension(2), intent(inout) :: Re
        real(wp), intent(out)                 :: Dth
        integer, intent(in)                   :: j, k, l
        real(wp)                              :: k_mix, rho_cv
        integer                               :: i

        #:if chemistry
            real(wp), dimension(num_species) :: Ys, Xs, Dk
            real(wp)                         :: T, cv, lam, W, mu
        #:endif

        Dth = 0._wp
        if (heat_conduction) then
            k_mix = 0._wp
            rho_cv = 0._wp
            $:GPU_LOOP(parallelism='[seq]')
            do i = 1, num_fluids
                k_mix = k_mix + alpha(i)*fluid_k_therm(i)
                rho_cv = rho_cv + alpha_rho(i)*cvs(i)
            end do
            Dth = k_mix/max(rho_cv, sgm_eps)
        end if

        #:if chemistry
            $:GPU_LOOP(parallelism='[seq]')
            do i = 1, num_species
                Ys(i) = q_prim_vf(eqn_idx%species%beg + i - 1)%sf(j, k, l)
            end do
            T = q_T_sf%sf(j, k, l)
            if (chem_params%diffusion) then
                call get_mixture_specific_heat_cv_mass(T, Ys, cv)
                if (chem_params%transport_model == 1) then
                    call get_mixavg_transport_state(pres, T, Ys, W, Xs, Dk, lam)
                    Dth = lam/(rho*cv)
                    $:GPU_LOOP(parallelism='[seq]')
                    do i = 1, num_species
                        Dth = max(Dth, Dk(i))
                    end do
                else
                    call get_mixture_thermal_conductivity_mixavg(T, Ys, lam)
                    Dth = lam/(rho*cv)
                end if
            end if
            if (viscous) then
                call get_mixture_viscosity_mixavg(T, Ys, mu)
                Re(1) = 1._wp/mu
            end if
        #:endif

    end subroutine s_compute_cell_diffusivity

    subroutine s_compute_stability_from_dt(vel, c, rho, Re_l, alpha, alpha_rho, Dth, j, k, l, icfl, vcfl, Rc, ccfl, tcfl)

        $:GPU_ROUTINE(parallelism='[seq]')
        real(wp), intent(in), dimension(num_vels) :: vel
        real(wp), intent(in)                      :: c, rho
        real(wp), intent(inout)                   :: icfl
        real(wp), intent(inout)                   :: vcfl, Rc, ccfl, tcfl
        real(wp), dimension(2), intent(in)        :: Re_l
        real(wp), intent(in)                      :: Dth
        #:if not MFC_CASE_OPTIMIZATION and USING_AMD
            real(wp), dimension(3), intent(in) :: alpha, alpha_rho
        #:else
            real(wp), dimension(num_fluids), intent(in) :: alpha, alpha_rho
        #:endif
        integer, intent(in) :: j, k, l
        real(wp)            :: fltr_dtheta
        real(wp)            :: rho_c

        ! Inviscid CFL calculation
        ! The multi-dimensional CFL terms are written out here rather than
        ! obtained from a shared helper procedure: NVHPC 25.5's fort2 segfaults
        ! when a routine containing a call to that helper is cross-file inlined
        ! by -Minline (the IPO setup in cmake/MFCTargets.cmake).
        if (p > 0) then
            #:if not MFC_CASE_OPTIMIZATION or num_dims > 2
                if (grid_geometry == 3) then
                    fltr_dtheta = f_compute_filtered_dtheta(k, l)
                    icfl = dt/min(dx(j)/(abs(vel(1)) + c), dy(k)/(abs(vel(2)) + c), fltr_dtheta/(abs(vel(3)) + c))
                else
                    icfl = dt/min(dx(j)/(abs(vel(1)) + c), dy(k)/(abs(vel(2)) + c), dz(l)/(abs(vel(3)) + c))
                end if
            #:endif
        else if (n > 0) then
            icfl = dt/min(dx(j)/(abs(vel(1)) + c), dy(k)/(abs(vel(2)) + c))
        else
            icfl = (dt/dx(j))*(abs(vel(1)) + c)
        end if

        ! Viscous calculations
        if (viscous) then
            vcfl = dt*f_visc_diffusivity(Re_l, rho)*f_inv_dx2_sum(j, k, l)
            if (p > 0) then
                #:if not MFC_CASE_OPTIMIZATION or num_dims > 2
                    if (grid_geometry == 3) then
                        fltr_dtheta = f_compute_filtered_dtheta(k, l)
                        Rc = min(dx(j)*(abs(vel(1)) + c), dy(k)*(abs(vel(2)) + c), fltr_dtheta*(abs(vel(3)) + c))/maxval(1._wp/Re_l)
                    else
                        Rc = min(dx(j)*(abs(vel(1)) + c), dy(k)*(abs(vel(2)) + c), dz(l)*(abs(vel(3)) + c))/maxval(1._wp/Re_l)
                    end if
                #:endif
            else if (n > 0) then
                Rc = min(dx(j)*(abs(vel(1)) + c), dy(k)*(abs(vel(2)) + c))/maxval(1._wp/Re_l)
            else
                Rc = dx(j)*(abs(vel(1)) + c)/maxval(1._wp/Re_l)
            end if
        end if

        ! Capillary CFL calculation
        if (surface_tension) then
            ccfl = 0._wp
            rho_c = f_capillary_rho(alpha, alpha_rho)
            if (rho_c > 0._wp) then
                if (p > 0) then
                    #:if not MFC_CASE_OPTIMIZATION or num_dims > 2
                        if (grid_geometry == 3) then
                            fltr_dtheta = f_compute_filtered_dtheta(k, l)
                            ccfl = dt*sqrt(2._wp*pi*sigma/(rho_c*min(dx(j), dy(k), fltr_dtheta)**3._wp))
                        else
                            ccfl = dt*sqrt(2._wp*pi*sigma/(rho_c*min(dx(j), dy(k), dz(l))**3._wp))
                        end if
                    #:endif
                else if (n > 0) then
                    ccfl = dt*sqrt(2._wp*pi*sigma/(rho_c*min(dx(j), dy(k))**3._wp))
                else
                    ccfl = dt*sqrt(2._wp*pi*sigma/(rho_c*dx(j)**3._wp))
                end if
            end if
        end if

        ! Thermal diffusion CFL
        tcfl = dt*Dth*f_inv_dx2_sum(j, k, l)

    end subroutine s_compute_stability_from_dt

    !> Computes the candidate dts for a specified CFL number: max_dt(1) from the inviscid, max_dt(2) the viscous, max_dt(3) the
    !! capillary, and max_dt(4) the thermal diffusion criterion (huge where the criterion is inactive)
    subroutine s_compute_dt_from_cfl(vel, c, max_dt, rho, Re_l, alpha, alpha_rho, Dth, j, k, l)

        $:GPU_ROUTINE(parallelism='[seq]')
        real(wp), dimension(num_vels), intent(in) :: vel
        real(wp), intent(in)                      :: c, rho
        real(wp), dimension(4), intent(out)       :: max_dt
        real(wp), dimension(2), intent(in)        :: Re_l
        real(wp), intent(in)                      :: Dth
        #:if not MFC_CASE_OPTIMIZATION and USING_AMD
            real(wp), dimension(3), intent(in) :: alpha, alpha_rho
        #:else
            real(wp), dimension(num_fluids), intent(in) :: alpha, alpha_rho
        #:endif
        integer, intent(in) :: j, k, l
        real(wp)            :: ccfl_dt, rho_c
        real(wp)            :: fltr_dtheta

        max_dt(2) = huge(1._wp)
        max_dt(3) = huge(1._wp)
        max_dt(4) = huge(1._wp)

        ! Inviscid CFL calculation
        ! The multi-dimensional CFL terms are written out here rather than
        ! obtained from a shared helper procedure: NVHPC 25.5's fort2 segfaults
        ! when a routine containing a call to that helper is cross-file inlined
        ! by -Minline (the IPO setup in cmake/MFCTargets.cmake).
        if (p > 0) then
            #:if not MFC_CASE_OPTIMIZATION or num_dims > 2
                if (grid_geometry == 3) then
                    fltr_dtheta = f_compute_filtered_dtheta(k, l)
                    max_dt(1) = cfl_target*min(dx(j)/(abs(vel(1)) + c), dy(k)/(abs(vel(2)) + c), fltr_dtheta/(abs(vel(3)) + c))
                else
                    max_dt(1) = cfl_target*min(dx(j)/(abs(vel(1)) + c), dy(k)/(abs(vel(2)) + c), dz(l)/(abs(vel(3)) + c))
                end if
            #:endif
        else if (n > 0) then
            max_dt(1) = cfl_target*min(dx(j)/(abs(vel(1)) + c), dy(k)/(abs(vel(2)) + c))
        else
            max_dt(1) = cfl_target*(dx(j)/(abs(vel(1)) + c))
        end if

        ! Viscous calculations
        if (viscous) max_dt(2) = cfl_target/(f_visc_diffusivity(Re_l, rho)*f_inv_dx2_sum(j, k, l))

        ! Capillary CFL calculations
        if (surface_tension) then
            ccfl_dt = huge(1._wp)
            rho_c = f_capillary_rho(alpha, alpha_rho)
            if (rho_c > 0._wp) then
                if (p > 0) then
                    #:if not MFC_CASE_OPTIMIZATION or num_dims > 2
                        if (grid_geometry == 3) then
                            fltr_dtheta = f_compute_filtered_dtheta(k, l)
                            ccfl_dt = cfl_target*sqrt(rho_c*min(dx(j), dy(k), fltr_dtheta)**3._wp/(2._wp*pi*sigma))
                        else
                            ccfl_dt = cfl_target*sqrt(rho_c*min(dx(j), dy(k), dz(l))**3._wp/(2._wp*pi*sigma))
                        end if
                    #:endif
                else if (n > 0) then
                    ccfl_dt = cfl_target*sqrt(rho_c*min(dx(j), dy(k))**3._wp/(2._wp*pi*sigma))
                else
                    ccfl_dt = cfl_target*sqrt(rho_c*dx(j)**3._wp/(2._wp*pi*sigma))
                end if
            end if
            max_dt(3) = ccfl_dt
        end if

        ! Thermal diffusion CFL: dt <= cfl/(D sum(1/dx^2))
        if (Dth > 0._wp) max_dt(4) = cfl_target/(Dth*f_inv_dx2_sum(j, k, l))

    end subroutine s_compute_dt_from_cfl

end module m_sim_helpers
